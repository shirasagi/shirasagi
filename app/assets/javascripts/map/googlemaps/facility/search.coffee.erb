class @Facility_Search
  @render :(selector, opts = {})->
    markers = opts["markers"]

    # define functions
    slideSidebar = (column) ->
      columnTop = column.offset().top
      indexTop  = column.closest("#map-sidebar").offset().top
      scrolled  = column.closest("#map-sidebar").scrollTop()
      column.closest("#map-sidebar").animate({ scrollTop: columnTop - indexTop + scrolled }, 'fast')

    # setup map
    Googlemaps_Map.load(selector)

    # setup markers
    overrided = Googlemaps_Map.attachMessage
    Googlemaps_Map.attachMessage = (id) ->
      overrided(id)
      google.maps.event.addListener Googlemaps_Map.markers[id]["marker"], 'click', (event) ->
        $("#map-sidebar .column").removeClass("current")
        dataID = Googlemaps_Map.markers[id]["id"]
        column = $('#map-sidebar .column[data-id="' + dataID + '"]')
        column.addClass("current")
        slideSidebar(column)
      google.maps.event.addListener Googlemaps_Map.markers[id]["window"], 'closeclick', (event) ->
        $("#map-sidebar .column").removeClass("current");
    Googlemaps_Map.setMarkers(markers)

    # setup sidebar
    $("#map-sidebar .column .click-marker").on "click", ->
      dataID = parseInt($(this).closest(".column").attr("data-id"))
      $("#map-sidebar .column").removeClass("current")
      $.each Googlemaps_Map.markers, (i, m) ->
        if dataID == m["id"]
          if Googlemaps_Map.openedInfo
            Googlemaps_Map.openedInfo.close()
          m["window"].open(m["marker"].getMap(), m["marker"]);
          Googlemaps_Map.openedInfo = m["window"]
          column = $('#map-sidebar .column[data-id="' + dataID + '"]')
          column.addClass("current")
          slideSidebar(column)
          return false
      return false

    # setup category filter
    $(".filters a").on "click", ->
      if $(this).hasClass("clicked")
        $(this).removeClass("clicked")
      else
        $(this).addClass("clicked")

      dataIDs = []
      $(".filters a.clicked").each ->
        dataIDs.push parseInt($(this).attr("data-id"))

      $.each Googlemaps_Map.markers, (id, value) ->
        visible = false
        $.each dataIDs, ->
          if $.inArray(parseInt(this), Googlemaps_Map.markers[id]["category"]) >= 0
            visible = true
            return false

        dataID = Googlemaps_Map.markers[id]["id"]
        column = $('#map-sidebar .column[data-id="' + dataID + '"]')
        if visible
          Googlemaps_Map.markers[id]["marker"].setVisible(true)
          column.show()
        else
          Googlemaps_Map.markers[id]["marker"].setVisible(false)
          Googlemaps_Map.markers[id]["window"].close()
          column.hide()
      return false

    # setup location filter
    $(".filters .focus").on "change", ->
      select = $(this)
      select.find("option:selected").each ->
        return false if $(this).val() == ""
        loc = $(this).val().split(",")
        zoomLevel = $(this).attr("data-zoom-level")
        latlng = new google.maps.LatLng(loc[0], loc[1])
        Googlemaps_Map.map.setCenter latlng
        Googlemaps_Map.map.setZoom(parseInt(zoomLevel)) if zoomLevel
        select.val("")
