class @Member_Photo_Form
  @maxPointForm   = 10
  @deleteMessage  = "<%= I18n.t('map.confirm.delete_marker') %>"
  @setExifMessage = "<%= I18n.t('map.confirm.find_exif') %>"
  @dataID         = 0
  @clickIcon      = "//maps.google.com/mapfiles/ms/icons/blue-dot.png"
  @clickMarker    = null

  @setMapLoc: (ele, lat, lon) ->
    lat = Math.ceil(lat * Math.pow(10,6)) / Math.pow(10,6)
    lon = Math.ceil(lon * Math.pow(10,6)) / Math.pow(10,6)
    ele.val(lat.toFixed(6) + "," + lon.toFixed(6))
    return

  @getMapLoc: (ele) ->
    latlon = ele.val().split(',')
    return new google.maps.LatLng(parseFloat(latlon[0]), parseFloat(latlon[1]))

  @attachMessage: (id) ->
    google.maps.event.addListener Googlemaps_Map.markers[id], 'click', (event) ->
      if(Googlemaps_Map.openedInfo)
        Googlemaps_Map.openedInfo.close()
      $('dd[data-id = "' + id + '"]').each ->
        name = $(this).find(".marker-name").val()
        text = $(this).find(".marker-text").val()
        if name != "" || text != ""
          marker_html = '<div class="marker-info">'
          if name != ""
            marker_html += '<p>' + name + '</p>'
          if text != ""
            $.each text.split(/[\r\n]+/), ->
              if this.match(/^https?:\/\//)
                marker_html += '<p><a href="' + this + '">' + this + '</a></p>'
              else
                marker_html += '<p>' + this + '</p>'

          Googlemaps_Map.openedInfo = new google.maps.InfoWindow(content: marker_html, maxWidth: 260)
          Googlemaps_Map.openedInfo.open(Googlemaps_Map.markers[id].getMap(), Googlemaps_Map.markers[id])
        return false
      return
    return

  @clearMarker: (ele)->
    dataId = 0
    if ele.val() != ""
      if confirm(Member_Photo_Form.deleteMessage)
        if Googlemaps_Map.markers[dataId]
          Googlemaps_Map.markers[dataId].setMap(null)
          ele.val("")
    else
      if Googlemaps_Map.markers[dataId]
        Googlemaps_Map.markers[dataId].setMap(null)
    return

  @createMarker: (ele)->
    if $(".mod-map .clicked").val() != ""
      ele.val($(".mod-map .clicked").val())
      dataId = 0

      Googlemaps_Map.markers[dataId].setMap(null) if Googlemaps_Map.markers[dataId]
      Googlemaps_Map.markers[dataId] = new google.maps.Marker(
        position: Member_Photo_Form.getMapLoc($(".mod-map .marker-loc")),
        map: Googlemaps_Map.map
      )
      Member_Photo_Form.attachMessage(dataId)
    return

  @renderMarkers: ()->
    markerBounds = new google.maps.LatLngBounds()

    if Googlemaps_Map.markers
      for m in Googlemaps_Map.markers
        m.setMap(null)
    Googlemaps_Map.markers = []
    Member_Photo_Form.dataID = 0

    $(".mod-map .marker").each ->
      $(this).attr("data-id", Member_Photo_Form.dataID)

      if $(this).find(".marker-loc").val() != ""
        loc = Member_Photo_Form.getMapLoc($(this).find(".marker-loc"))
        Googlemaps_Map.markers[Member_Photo_Form.dataID] = new google.maps.Marker(
          position: loc,
          map: Googlemaps_Map.map
        )
        Member_Photo_Form.attachMessage(Member_Photo_Form.dataID)
        markerBounds.extend(loc)
      Member_Photo_Form.dataID += 1

    if Googlemaps_Map.markers.length > 0
      google.maps.event.addListenerOnce Googlemaps_Map.map, 'idle', (event) ->
        if this.getZoom() > 13
          this.setZoom(13)

      Googlemaps_Map.map.fitBounds(markerBounds)
    return

  @setExifLatLng: (selector)->
    $(selector).change (e)->
      return unless e.target.files[0]

      EXIF.getData e.target.files[0], ->
        lat    = EXIF.getTag(this, 'GPSLatitude')
        lon    = EXIF.getTag(this, 'GPSLongitude')
        latRef = EXIF.getTag(this, 'GPSLatitudeRef') || "N"
        lonRef = EXIF.getTag(this, 'GPSLongitudeRef') || "W"
        return false unless lat && lon
        return false unless confirm(Member_Photo_Form.setExifMessage)

        latRef = if latRef == "N" then 1 else -1
        lonRef = if lonRef == "W" then -1 else 1
        lat = (lat[0] + (lat[1] / 60) + (lat[2] / 3600)) * latRef
        lon = (lon[0] + (lon[1] / 60) + (lon[2] / 3600)) * lonRef
        $(".mod-map .clicked").val([lat, lon].join())
        Member_Photo_Form.createMarker($(".mod-map .marker-loc"))
        Googlemaps_Map.map.setCenter(new google.maps.LatLng(lat, lon))

  @renderEvents: ()->
    google.maps.event.addListener Googlemaps_Map.map, 'click', (event) ->
      Member_Photo_Form.setMapLoc($(".mod-map .clicked"), event.latLng.lat(), event.latLng.lng())
      Member_Photo_Form.createMarker($(".mod-map .marker-loc"))

    $(".mod-map .clear-marker").on 'click', (e)->
      Member_Photo_Form.clearMarker($(".mod-map .marker-loc"))
      return false
