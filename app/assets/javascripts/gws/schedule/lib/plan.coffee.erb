class @Gws_Schedule_Plan
  @diffOn: 3600000

  @renderForm: (opts = {})->
    $("input.date").datetimepicker
      lang: "ja"
      timepicker: false
      format:'Y/m/d'
      closeOnDateSelect: true
      scrollInput: false
      maxDate: opts["maxDate"]
    $("input.datetime").datetimepicker
      lang: "ja"
      roundTime: 'ceil'
      step: 30
      maxDate: opts["maxDate"]
    @relateDateForm()
    @relateDateTimeForm()

  @renderAlldayForm: ->
    @changeDateForm()
    $('#item_allday').change ->
      Gws_Schedule_Plan.changeDateValue()
      Gws_Schedule_Plan.changeDateForm()

  # @example
  #   2015/09/29 00:00 => 2015/09/29
  #   2015/09/29 => 2015/09/29 00:00
  @changeDateValue: ->
    if $('#item_allday').prop('checked')
      $('#item_start_on').val $('#item_start_at').val().replace(/ .*/, '')
      $('#item_end_on').val $('#item_end_at').val().replace(/ .*/, '')
    else
      stime = $('#item_start_at').val().replace(/.* /, '')
      etime = $('#item_end_at').val().replace(/.* /, '')
      stime = '00:00' if stime == '' && $('#item_start_on').val() != ''
      etime = '00:00' if etime == '' && $('#item_end_on').val() != ''
      $('#item_start_at').val $('#item_start_on').val() + " #{stime}"
      $('#item_end_at').val $('#item_end_on').val() + " #{etime}"

  @changeDateForm: ->
    if $('#item_allday').prop('checked')
      $('.dates-field').show()
      $('.datetimes-field').hide()
    else
      $('.dates-field').hide()
      $('.datetimes-field').show()

  @relateDateForm: (start_sel = '.date.start', end_sel = '.date.end') ->
    $("#{start_sel}, #{end_sel}").click ->
      Gws_Schedule_Plan.diffOn = Gws_Schedule_Plan.diffDates($(start_sel).val(), $(end_sel).val())
    $(start_sel).change ->
      start = $(start_sel).val()
      return unless start
      start = (new Date(start)).getTime()
      return if isNaN(start)
      date = new Date()
      date.setTime(start + Gws_Schedule_Plan.diffOn)
      format = '%d/%02d/%02d'
      format = '%d/%02d/%02d %02d:%02d' if $(start_sel).hasClass('datetime')
      $(end_sel).val(sprintf(format, date.getFullYear(), date.getMonth() + 1, date.getDate(), date.getHours(), date.getMinutes()))
    if $(end_sel).val() == ""
      $(start_sel).change()

  @relateDateTimeForm: ->
    @relateDateForm('.datetime.start', '.datetime.end')

  @diffDates: (src, dst)->
    return 1000 * 60 * 60 if !src || !dst
    diff = (new Date(dst)).getTime() - (new Date(src)).getTime();
    return 0 if diff < 0
    diff
