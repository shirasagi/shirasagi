class @Gws_Schedule_Calendar
  @render: (selector, opts = {})->
    params = @defaultParams(opts)
    $.extend true, params, @editableParams(opts) if opts['restUrl']
    $.extend true, params, @tapMenuParams(opts) if opts['restUrl']
    $.extend true, params, opts
    $(selector).fullCalendar(params)

    $(document).click (ev) ->
      $(".fc-event").not($(ev.target).closest(".fc-event")).find(".fc-popup").remove()

  @defaultParams: (opts) ->
    columnFormat: { month: "ddd", week: "M/D [(]ddd[)]", day: "M/D [(]ddd[)]" }
    contentHeight: 'auto'
    endParam: 's[end]'
    fixedWeekCount: false
    header: { left: 'today prev next title', right: 'month basicWeek agendaDay' },
    lang: 'ja'
    nextDayThreshold: '00:00:00' # 複数日表示の閾値
    schedulerLicenseKey: 'CC-Attribution-NonCommercial-NoDerivatives'
    slotLabelFormat: 'HH:mm',
    startParam: 's[start]'
    timeFormat: 'HH:mm'
    titleFormat:
      month: "<%= I18n.t('gws/schedule.calendar.titleFormat.month') %>"
      week: "<%= I18n.t('gws/schedule.calendar.titleFormat.week') %>"
      day: "<%= I18n.t('gws/schedule.calendar.titleFormat.day') %>"
    loading: (isLoading, view) ->
      if isLoading
        $(this).find('.fc-day').eq(0).append('<span class="fc-loading"><%= I18n.t('gws/schedule.loading') %></span>')
      else
        $(this).find('.fc-loading').remove()

  @tapMenuParams: (opts) ->
    url = opts['restUrl']

    dayClick: (date, event, view) ->
      if opts['tapMenu']
        now = new Date
        start = "#{date.format()}T#{now.getHours()}:00:00"
        links  = "<a href='#{url}/new?start=#{start}' class='add-plan'><%= I18n.t 'gws/schedule.links.add_plan' %></a>"
        #if opts['defaultView'] != 'basicWeek'
        #  if view.name != 'month'
        #    links += '<a href="" data-view="month"><%= I18n.t "gws/schedule.links.show_month" %></a>'
        #  if view.name != 'basicWeek'
        #    links += '<a href="" data-view="basicWeek"><%= I18n.t "gws/schedule.links.show_week" %></a>'
        #  if view.name != 'agendaDay'
        #    links += '<a href="" data-view="agendaDay"><%= I18n.t "gws/schedule.links.show_day" %></a>'
        $("body").append('<div class="tap-menu">' + links + '</div>')
        $(".tap-menu").show().css("left", event.pageX - 5).css("top", event.pageY - 5).show()

        $(".tap-menu a").click ->
          if $(this).data('view')
            cal = view.calendar.getCalendar()
            cal.changeView($(this).data('view'));
            cal.gotoDate(date);
            $(".tap-menu").remove()
            return false
        $(".tap-menu").mouseleave ->
          $(".tap-menu").remove()

  @editableParams: (opts)->
    url = opts['restUrl']

    editable: true,
    eventClick: (event, jsEvent, view) ->
      return if $(this).hasClass('fc-holiday')

      popup = "fc-popup"
      if $(this).find(".#{popup}").length == 0
        $(this).append("<div class='#{popup}'><span class='fc-loading'>Loading..</span></div>")

      view = $(this).find(".#{popup}")
      hgt = view.outerHeight()
      if $(this).offset().top - hgt < 0
        view.addClass("position-bottom")
        view.css("bottom", (hgt * (-1) - 15) + "px")
        $(".#{popup} style").replaceWith("<style>.#{popup}:after { bottom: " + (hgt - 5) + "px; }</style>")
      else
        view.addClass("position-top")
        view.css("bottom", "20px")
        $(".#{popup} style").replaceWith("<style>.#{popup}:after { bottom: -13px; }</style>")

      $.ajax
        url: "#{url}/#{event.id}/popup"
        success: (data)->
          view.html(data)

    eventDrop: (event, delta, revertFunc, jsEvent, ui, view) ->
      end = null
      end = event.end.format() if event.end
      $.ajax
        type: 'PUT'
        url: "#{url}/" + event.id + ".json"
        data: { item: { api: 'drop', api_start: event.start.format(), api_end: end } }
        success: (data, dataType) ->
          viewId = view.el.closest('.calendar').attr('id')
          $('.calendar.multiple').not("##{viewId}").fullCalendar('refetchEvents')
        error: (xhr, status, error) ->
          alert xhr.responseJSON.join("\n")
          revertFunc()
    eventResize: (event, delta, revertFunc) ->
      $.ajax
        type: 'PUT'
        url: "#{url}/" + event.id + ".json"
        data: { item: { api: 'resize', api_start: event.start.format(), api_end: event.end.format() } }
        success: (data, dataType) ->
          viewId = view.el.closest('.calendar').attr('id')
          $('.calendar.multiple').not("##{viewId}").fullCalendar('refetchEvents')
        error: (xhr, status, error) ->
          alert xhr.responseJSON.join("\n")
          revertFunc()
