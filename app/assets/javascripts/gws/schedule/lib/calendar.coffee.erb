class @Gws_Schedule_Calendar
  @messages:
    noPlan: '<%= I18n.t('gws/schedule.no_plan') %>'

  @render: (selector, opts = {}, init = {})->
    params = @defaultParams(selector, opts)
    $.extend true, params, @editableParams(opts) if opts['restUrl']
    $.extend true, params, @tapMenuParams(opts) if opts['restUrl']
    $.extend true, params, opts
    $(selector).fullCalendar(params)
    @renderInitialize(selector, init)
    @overrideAddLink(selector)

  @renderInitialize: (selector, init = {})->
    init['viewTodo'] ||= 'active'

    $(selector).fullCalendar('changeView', init['view']) if init['view']
    $(selector).fullCalendar('gotoDate', init['date']) if init['date']

    if init['viewFormat'] == 'list'
      $.fullCalendar.toggleListFormat(selector)
      $(selector).find('.fc-withListView-button').addClass("fc-state-active")

    if init['viewTodo'] == 'active'
      $(selector).find('.fc-withTodo-button').addClass("fc-state-active")

    if init['viewAttendance'] == 'active'
      $(selector).find('.fc-withAbsence-button').addClass("fc-state-active")

    Gws_Schedule_View.renderSideCalendars(selector)

    $("#{selector}-header .calendar-text").each ->
      wrap = $(this)
      data = $(this).find('.calendar-text-popup').prop('outerHTML')
      wrap.find('.calendar-text-link').click ->
        Gws_Popup.render($(this), $(data).show())
        return false

  @defaultParams: (selector, opts) ->
    buttonText:
      listMonth: "<%= I18n.t('gws/schedule.calendar.buttonText.listMonth') %>"
    columnFormat: { month: "ddd", week: "M/D [(]ddd[)]", day: "M/D [(]ddd[)]" }
    customButtons:
      withTodo:
        text: "<%= I18n.t('gws/schedule.calendar.buttonText.withTodo') %>"
        click: (e) ->
          $('.fc-event-todo').toggle(!$(this).hasClass('fc-state-active'))
          $(this).toggleClass("fc-state-active")
          Gws_Schedule_Calendar.updateNoPlanVisibility($(this).closest(".fc"))
      withAbsence:
        text: "<%= I18n.t('gws/schedule.calendar.buttonText.withAbsence') %>"
        click: (e) ->
          $('.fc-event-user-attendance-absence').toggle(!$(this).hasClass('fc-state-active'))
          $(this).toggleClass("fc-state-active")
          Gws_Schedule_Calendar.updateNoPlanVisibility($(this).closest(".fc"))
      withListView:
        text: "<%= I18n.t('gws/schedule.calendar.buttonText.listMonth') %>"
        click: (e) ->
          $.fullCalendar.toggleListFormat(selector)
          $(selector).fullCalendar('refetchEvents')
          $(window).trigger('resize') # for AgendaView
          $(this).toggleClass("fc-state-active")
    contentHeight: 'auto'
    displayEventEnd:
      basicWeek: true
    endParam: 's[end]'
    fixedWeekCount: false
    slotEventOverlap: false
    header: { left: 'today prev next title', right: 'withAbsence withTodo month,basicWeek,agendaDay withListView' },
    lang: 'ja'
    nextDayThreshold: '00:00:00' # 複数日表示の閾値
    schedulerLicenseKey: 'CC-Attribution-NonCommercial-NoDerivatives'
    slotLabelFormat: 'HH:mm',
    startParam: 's[start]'
    timeFormat: 'HH:mm'
    titleFormat:
      month: "<%= I18n.t('gws/schedule.calendar.titleFormat.month') %>"
      week: "<%= I18n.t('gws/schedule.calendar.titleFormat.week') %>"
      day: "<%= I18n.t('gws/schedule.calendar.titleFormat.day') %>"
    loading: (isLoading, view) ->
      if isLoading
        if $(selector).hasClass("fc-list-format")
          $(this).find('.fc-view').prepend('<span class="fc-loading"><%= I18n.t('gws/schedule.loading') %></span>')
        else
          $(this).find('.fc-widget-content').eq(0).prepend('<span class="fc-loading"><%= I18n.t('gws/schedule.loading') %></span>')
      else
        $(this).find('.fc-loading').remove()
    eventAfterAllRender: (view) ->
      todo = $('.fc .fc-withTodo-button').hasClass('fc-state-active')
      if todo
        $('.fc .fc-event-todo').show()
      else
        $('.fc .fc-event-todo').hide()

      attendance = $('.fc .fc-withAbsence-button').hasClass('fc-state-active')
      if attendance
        $('.fc .fc-event-user-attendance-absence').show()
      else
        $('.fc .fc-event-user-attendance-absence').hide()

      Gws_Schedule_Calendar.updateNoPlanVisibility(view.el.closest(".fc"))
      Gws_Schedule_Calendar.changePrintPreviewPortrait(view)

  @tapMenuParams: (opts) ->
    url = opts['restUrl']

    dayClick: (date, event, view) ->
      links = ''

      if opts['tapMenu']
        now = new Date
        start = "#{date.format()}T#{now.getHours()}:00:00"
        view_format = if view.el.closest(".fc").hasClass('fc-list-format') then 'list' else 'default'
        view_todo = if $('.fc .fc-withTodo-button').hasClass("fc-state-active") then 'active' else 'inactive'
        view_attendance = if $('.fc .fc-withAbsence-button').hasClass("fc-state-active") then 'active' else 'inactive'
        state = "calendar[date]=#{date.format()}&calendar[view]=#{view.name}&calendar[viewFormat]=#{view_format}&calendar[viewTodo]=#{view_todo}&calendar[viewAttendance]=#{view_attendance}"
        links = "<a href='#{url}/new?start=#{start}&#{state}' class='add-plan'><%= I18n.t 'gws/schedule.links.add_plan' %></a>"

      if $('#calendar-controller').length == 0
        if view.name != 'month'
          links += '<a href="" data-view="month"><%= I18n.t "gws/schedule.links.show_month" %></a>'
        if view.name != 'basicWeek'
          links += '<a href="" data-view="basicWeek"><%= I18n.t "gws/schedule.links.show_week" %></a>'
        if view.name != 'agendaDay'
          links += '<a href="" data-view="agendaDay"><%= I18n.t "gws/schedule.links.show_day" %></a>'

      if links
        $("body").append('<div class="tap-menu">' + links + '</div>')

        if event.pageX + $(".tap-menu").width() > $(window).width()
          $(".tap-menu").css("top", event.pageY - 5).css("right", 5).show()
        else
          $(".tap-menu").css("top", event.pageY - 5).css("left", event.pageX - 5).show()

        $(".tap-menu a").click ->
          if $(this).data('view')
            cal = view.calendar.getCalendar()
            cal.changeView($(this).data('view'));
            cal.gotoDate(date);
            $(".tap-menu").remove()
            return false

        $(".tap-menu").mouseleave ->
          $(".tap-menu").remove()

  @editableParams: (opts)->
    url = opts['restUrl']

    editable: true,
    eventClick: (event, jsEvent, view) ->
      return if event.noPopup

      target = $(this)
      Gws_Popup.render(target, "<div class='fc-popup'><span class='fc-loading'><%= I18n.t('gws/schedule.loading') %></span></div>")

      popup_url = if event.restUrl then event.restUrl else url
      view_format = if view.el.closest(".fc").hasClass('fc-list-format') then 'list' else 'default'
      view_todo = if $('.fc .fc-withTodo-button').hasClass("fc-state-active") then 'active' else 'inactive'
      view_attendance = if $('.fc .fc-withAbsence-button').hasClass("fc-state-active") then 'active' else 'inactive'
      state = "calendar[date]=#{event.start.format('YYYY-MM-DD')}&calendar[view]=#{view.name}&calendar[viewFormat]=#{view_format}&calendar[viewTodo]=#{view_todo}&calendar[viewAttendance]=#{view_attendance}"

      $.ajax
        url: "#{popup_url}/#{event.id}/popup"
        success: (data)->
          $('.fc-popup').html(data)
          $('.fc-popup').find('a').each ->
            $(this).attr('href', $(this).attr('href') + "?#{state}")
          Gws_Popup.renderPopup(target)

    eventDrop: (event, delta, revertFunc, jsEvent, ui, view) ->
      end = null
      end = event.end.format() if event.end
      drop_url = if event.restUrl then event.restUrl else url
      $.ajax
        type: 'PUT'
        url: "#{drop_url}/" + event.id + ".json"
        data: { item: { api: 'drop', api_start: event.start.format(), api_end: end } }
        success: (data, dataType) ->
          viewId = view.el.closest('.calendar').attr('id')
          $('.calendar.multiple').not("##{viewId}").fullCalendar('refetchEvents')
        error: (xhr, status, error) ->
          alert xhr.responseJSON.join("\n")
          revertFunc()

    eventResize: (event, delta, revertFunc) ->
      $.ajax
        type: 'PUT'
        url: "#{url}/" + event.id + ".json"
        data: { item: { api: 'resize', api_start: event.start.format(), api_end: event.end.format() } }
        success: (data, dataType) ->
          viewId = view.el.closest('.calendar').attr('id')
          $('.calendar.multiple').not("##{viewId}").fullCalendar('refetchEvents')
        error: (xhr, status, error) ->
          alert xhr.responseJSON.join("\n")
          revertFunc()

  @changePrintPreviewPortrait: (view) ->
    if $('body').hasClass('print-preview')
      if view.type == 'agendaDay' || view.type == 'listMonth' || view.el.closest(".fc").hasClass("fc-list-format")
        $('body').removeClass('horizontal')
        $('body').addClass('vertical')
      else
        $('body').removeClass('vertical')
        $('body').addClass('horizontal')

  @overrideAddLink: (selector) ->
    $('.add-plan').click (ev) ->
      now  = new Date
      date = $(selector).fullCalendar('getDate')
      view = $(selector).fullCalendar('getView')
      href = $(this).attr('href').replace(/\?.*/, '')

      unless view.start.isBefore(now) && view.end.isAfter(now)
        start = "#{date.format('YYYY-MM-DD')}T#{now.getHours()}:00:00"
        view_format = if view.el.closest(".fc").hasClass('fc-list-format') then 'list' else 'default'
        view_todo = if $('.fc .fc-withTodo-button').hasClass("fc-state-active") then 'active' else 'inactive'
        view_attendance = if $('.fc .fc-withAbsence-button').hasClass("fc-state-active") then 'active' else 'inactive'
        state = "calendar[date]=#{date.format()}&calendar[view]=#{view.name}&calendar[viewFormat]=#{view_format}&calendar[viewTodo]=#{view_todo}&calendar[viewAttendance]=#{view_attendance}"
        href  = href + "?start=#{start}&#{state}"
      $(this).attr('href', href)

  @updateNoPlanVisibility: (selector) ->
    no_plan = $(selector).find('.fc-listMonth-view-container .no-plan')
    if no_plan.length != 0
      if $('.fc-event:visible').length == 0
        no_plan.show()
      else
        no_plan.hide()
