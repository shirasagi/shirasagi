class @Gws_Bookmark
  @render: (opts = {})->
    el = $('.gws-bookmark')
    el.click (e) ->
      if el.find('img[src="/assets/img/loading.gif"]').length > 0
        return false
      else if $(e.target).hasClass('update')
        Gws_Bookmark.update(el, opts)
      else if $(e.target).hasClass('delete')
        Gws_Bookmark.delete(el, opts)
      else if el.find('.bookmark-id').val()
        el.addClass('active')
        el.find('.dropdown-menu').addClass('active')
      else
        Gws_Bookmark.create(el, opts)

  @create: (el, opts = {})->
    name = $('title').text().split(' | ')[0]
    html = el.html()
    el.find('.dropdown-menu').html(SS.loading)
    $.ajax
      url: opts['url']
      method: 'POST'
      data:
        item:
          name: name
          url: location.pathname
          model: opts['model']
      success: (data)->
        el.html(html)
        el.addClass('active')
        el.find('.dropdown-menu').addClass('active')
        el.find('.bookmark-notice').text('<%= I18n.t('gws/bookmark.notice.save') %>')
        el.find('.bookmark-id').val(data['bookmark_id'])
        el.find('.bookmark-name').val(name)
        el.find('.material-icons').html("&#xE866;")
      error: (data)->
        alert('Error')

  @update: (el, opts = {})->
    name = el.find('.bookmark-name').val() || $('title').text().split(' | ')[0]
    url = opts['url'] + '/' + el.find('.bookmark-id').val()
    html = el.html()
    el.find('.dropdown-menu').html(SS.loading)
    el.addClass('active')
    el.find('.dropdown-menu').addClass('active')
    $.ajax
      url: url
      method: 'POST'
      data:
        _method: 'patch'
        item:
          name: name
          url: location.pathname
          model: opts['model']
      success: (data)->
        el.html(html)
        el.removeClass('active')
        el.find('.dropdown-menu').removeClass('active')
        el.find('.bookmark-notice').text('<%= I18n.t('gws/bookmark.notice.save') %>')
        el.find('.bookmark-name').val(data['name'])
        el.find('.material-icons').html("&#xE866;")
      error: (data)->
        alert('Error')

  @delete: (el, opts = {})->
    unless el.find('.bookmark-id').val()
      return false
    name = $('title').text().split(' | ')[0]
    url = opts['url'] + '/' + el.find('.bookmark-id').val()
    html = el.html()
    el.find('.dropdown-menu').html(SS.loading)
    el.addClass('active')
    el.find('.dropdown-menu').addClass('active')
    $.ajax
      url: url
      method: 'POST'
      data:
        _method: 'delete'
        item:
          url: location.pathname
      success: (data)->
        el.html(html)
        el.removeClass('active')
        el.find('.dropdown-menu').removeClass('active')
        el.find('.bookmark-notice').text('')
        el.find('.bookmark-id').val('')
        el.find('.bookmark-name').val(name)
        el.find('.material-icons').html("&#xE867;")
      error: (data)->
        alert('Error')
