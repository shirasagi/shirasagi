class @SS_SearchUI
  @anchorAjaxBox

  @select: (item) ->
    self = this

    data = item.closest("[data-id]")
    id   = data.data("id")
    name = data.data("name") || data.find(".select-item").text() || item.text() || data.text()

    tr    = $("<tr />").attr("data-id", id) #attr
    input = self.anchorAjaxBox.closest("dl").find(".hidden-ids").clone(false)
    input = input.val(id).removeClass("hidden-ids")
    a     = $('<a class="deselect btn" href="#"><%= I18n.t "views.buttons.delete" %></a>').on "click", self.deselect

    tr.append($('<td />').append(input).append(name))
    tr.append($('<td />').append(a))
    self.anchorAjaxBox.closest("dl").find(".ajax-selected tbody").prepend(tr)

  @selectItems: ->
    self = this

    $("#ajax-box .items input:checkbox").filter(":checked").each ->
      self.select($(this))
    self.anchorAjaxBox.closest("dl").find(".ajax-selected").show()

  @deselect: (e)->
    table = $(this).parents(".ajax-selected:first")
    $(this).parents("tr:first").remove()
    if table.find("tbody tr").size() == 0
      table.hide()
    e.preventDefault()

  @toggleSelectButton: ()->
    if $("#ajax-box .items input:checkbox").filter(":checked").size() > 0
      $("#ajax-box .select-items").parent("div").show()
    else
      $("#ajax-box .select-items").parent("div").hide()

  @render: ()->
    self = this

    $(".ajax-selected").each ->
      $(this).find("a.deselect").on "click", self.deselect
      if $(this).find("a.deselect").size() == 0
        $(this).hide()

  @modal: (options = {})->
    self = this

    if $.colorbox.element().hasClass("ajax-box")
      self.anchorAjaxBox = $.colorbox.element()

    $("#ajax-box form.search").on "submit", (e)->
      $(this).ajaxSubmit(
        url: $(this).attr("action")
        success: (data) ->
          $("#cboxLoadedContent").html data
        error: (data, status) ->
          alert("== Error ==")
      )
      e.preventDefault()

    $("#ajax-box .pagination a").on "click", (e)->
      self.selectItems()

    submit_on_changes = options.submit_on_changes
    submit_on_changes = [ "#s_group" ] unless submit_on_changes
    for el in submit_on_changes
      $("#ajax-box #{el}").on "change", (e)->
        self.selectItems()
        $("#ajax-box form.search").submit()

    $("#ajax-box .submit-on-change").on "change", (e)->
      self.selectItems()
      $("#ajax-box form.search").submit()

    self.anchorAjaxBox.closest("dl").find(".ajax-selected tr[data-id]").each ->
      id = $(this).data("id")
      tr = $("#colorbox .items [data-id='#{id}']")
      tr.find("input[type=checkbox]").remove()
      item = tr.find(".select-item,.select-single-item").html()
      tr.find(".select-item,.select-single-item").replaceWith("<span class='select-item' style='color: #888'>#{item}</span>")

    SS_ListUI.render("table.index")

    $("#ajax-box a.select-item").on "click", (e)->
      return false unless SS.disableClick($(this))
      # append newly selected item
      self.select($(this))
      self.anchorAjaxBox.closest("dl").find(".ajax-selected").show()
      e.preventDefault()
      $.colorbox.close()

    $("#ajax-box .select-single-item").on "click", (e)->
      return false unless SS.disableClick($(this))
      # remove old items
      self.anchorAjaxBox.closest("dl").find(".ajax-selected tr[data-id]").each ->
        if $(this).find("input[value]").length
          $(this).remove()
      # append newly selected item
      self.select($(this))
      self.anchorAjaxBox.closest("dl").find(".ajax-selected").show()
      e.preventDefault()
      $.colorbox.close()

    $("#ajax-box .select-items").on "click", (e)->
      return false unless SS.disableClick($(this))
      self.selectItems()
      e.preventDefault()
      $.colorbox.close()

    $("#ajax-box .index").on "change", (e)->
      self.toggleSelectButton()
    self.toggleSelectButton()
