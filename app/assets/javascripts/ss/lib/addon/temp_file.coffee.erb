class @SS_Addon_TempFile
  @select = null
  @userId = null
  @dropEventTriggered = null

  @selectUrl: (id)->
    return "/.u#{@userId}/apis/temp_files/#{id}/select"

  @uploadUrl: ()->
    return "/.u#{@userId}/apis/temp_files.json"

  @renderDrop: (selector, userId)->
    @userId = userId

    if !@select
      @select = (files, dropArea)->
        for file in files
          id = file["_id"]
          url = SS_Addon_TempFile.selectUrl(id)
          $.ajax
            url: url,
            success: (data) ->
              $("#selected-files").append(data)

    $(selector).each (i, da)->
      da = $(da)
      da.on 'dragenter', (e) ->
        da.addClass('file-dragenter')
        return false
      da.on 'dragleave', (e) ->
        da.removeClass('file-dragenter')
        return false
      da.on 'dragover', (e) ->
        da.addClass('file-dragenter')
        return false
      da.on 'drop', (e) ->
        dropArea = e.target
        token = $('meta[name="csrf-token"]').attr('content')
        formData = new FormData()
        formData.append('authenticity_token', token)
        files = e.originalEvent.dataTransfer.files
        return false if files.length == 0

        if SS_Addon_TempFile.dropEventTriggered
          return false
        SS_Addon_TempFile.dropEventTriggered = true

        for file in files
          formData.append('item[in_files][]', file)
        request = new XMLHttpRequest()
        #request.upload.addEventListener 'progress', (e) ->
        #  return false
        request.onload = (e) ->
          if request.readyState == XMLHttpRequest.DONE
            da.removeClass('file-dragenter')
            if request.status == 200 || request.status == 201
              files = JSON.parse(request.response)
              SS_Addon_TempFile.select(files, dropArea)
            else if request.status == 413
              alert(["== Error =="].concat("<%= I18n.t('errors.messages.request_entity_too_large') %>").join("\n"))
            else
              try
                json = $.parseJSON(request.response)
                alert(["== Error =="].concat(json).join("\n"))
              catch
                alert(["== Error =="].concat(request.statusText).join("\n"))
            SS_Addon_TempFile.dropEventTriggered = false

        request.open("POST", SS_Addon_TempFile.uploadUrl())
        request.send(formData)
        return false
